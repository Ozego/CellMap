#pragma kernel Fade
#pragma kernel Clear
#pragma kernel AlignLines
#pragma kernel DrawLine
#pragma kernel MoveLines
#include "rngInclude.compute"

struct Line
{
    float2 _start;
    float2 _end;
    float4 _color;
};

RWStructuredBuffer<Line> LineBuffer;
RWTexture2D<float4> OutTexture;
int width;
int height;
int frame;
float chaos;

[numthreads(8,8,1)]
void Fade (uint3 id : SV_DispatchThreadID)
{
    float4 col = OutTexture[id.xy];
    col.rgb*=.999;
    col.rgb *= col.rgb;
    col.a=1.;
    OutTexture[id.xy]=col;
}
[numthreads(8,8,1)]
void Clear (uint3 id : SV_DispatchThreadID)
{
    OutTexture[id.xy]=float4(1.,.9,.1,1.);
}
[numthreads(8,1,1)]
void MoveLines (uint3 id : SV_DispatchThreadID)
{
    Line l = LineBuffer[id.x];
    float2 movement = l._end - l._start;
    l._start += movement ;
    l._end   += abs(length(movement)+(rngFloat(id.x+1,frame)-.5)*chaos) * normalize(movement + rngVector2(frame+id.x,id.x)*chaos);
    LineBuffer[id.x] = l;
};
[numthreads(8,1,1)]
void AlignLines (uint3 id : SV_DispatchThreadID)
{
    Line l = LineBuffer[id.x];
    l._end = l._start + rngVector2(frame,0)*rngFloat(frame,1)*20.;
    LineBuffer[id.x] = l;
};

[numthreads(8,1,1)]
void DrawLine (uint3 id : SV_DispatchThreadID)
{
    Line l = LineBuffer[id.x];

    int w = l._end.x - l._start.x ;
    int h = l._end.y - l._start.y ;
    
    // line direction
    float4 col = float4
    (
        normalize( float2( (float)w, (float)h )) * .5 + float2( .5, .5 ),
        (float)frame/256.,
        1.
    );

    int dx1 = sign(w), dx2 = dx1, dy1 = sign(h), dy2 = 0 ;
    int longest = abs(w) ;
    int shortest = abs(h) ;
    if (!(longest>shortest)) 
    {
        longest = abs(h) ;
        shortest = abs(w) ;
        dy2 = dy1;
        dx2 = 0;
    }
    int numerator = longest >> 1 ; // bitwise operation: longest/2 floored
    for (int i=0, x = l._start.x, y = l._start.y;i<=longest;i++) 
    {
        int modX = (x%width+width)%width;
        int modY = (y%height+height)%height;
        OutTexture[int2(modX,modY)] = l._color;//col;
        numerator += shortest ;
        if (!(numerator<longest)) {
            numerator -= longest ;
            x += dx1 ;
            y += dy1 ;
        } else {
            x += dx2 ;
            y += dy2 ;
        }
    }
}
